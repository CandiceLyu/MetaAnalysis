# 4. Pair-Wise Meta-Analysis Models

# 4.1 Fixed Effect Model
# Model specification
model{
for (i in 1:ns){
      		# Define the log of the probability for intervention 1
		mu[i] <- log(p1[i])
      		# Prior distribution for the probability of an event in intervention 1
		p1[i] ~ dunif(0, 1)

      		# Binomial likelihood: observed events in two intervention groups
		r1[i] ~ dbin(p1[i], n1[i])
		r2[i] ~ dbin(p2[i], n2[i])

      		# Expected number of events
		rhat1[i] <- p1[i] * n1[i]
		rhat2[i] <- p2[i] * n2[i]

		# Model the log of the probability for intervention 2 based on intervention 1
		log(p2[i]) <- mu[i] + min(d, -mu[i])

     		# Deviance for model fit diagnostics, exclude the last study
		dev1[i] <- (1 - equals(i, ns)) * (2 * (r1[i] * (log(r1[i]) - log(rhat1[i])) + (n1[i] - r1[i]) * (log(n1[i] - r1[i]) - log(n1[i] - rhat1[i]))))
		dev2[i] <- (1 - equals(i, ns)) * (2 * (r2[i] * (log(r2[i]) - log(rhat2[i])) + (n2[i] - r2[i]) * (log(n2[i] - r2[i]) - log(n2[i] - rhat2[i]))))
		resdev[i] <- dev1[i] + dev2[i]
		}

# Total residual deviance for model fit diagnostics
totresdev <- sum(resdev[])
# Prior distribution for the log risk ratio
d ~ dnorm(0, 0.0001)
# Calculate the risk ratio
RR <- exp(d)
}

############################################################################################################################################################################
############################################################################################################################################################################

# 4.2 Random Effect Model
# Model specification
model{
for (i in 1:ns){
      		# Define the log of the probability for intervention 1
		mu[i] <- log(p1[i])
      		# Prior distribution for the probability of an event in intervention 1
		p1[i] ~ dunif(0, 1)

		# Binomial likelihood: observed events in two intervention groups
		r1[i] ~ dbin(p1[i], n1[i])
		r2[i] ~ dbin(p2[i], n2[i])

		# Expected number of events
		rhat1[i] <- p1[i] * n1[i]
		rhat2[i] <- p2[i] * n2[i]

      		# Model the log of the probability for intervention 2 based on intervention 1 with study-specific effects
		log(p2[i]) <- mu[i] + min(delta[i], -mu[i])
		delta[i] ~ dnorm(d, prec)

      		# Deviance for model fit diagnostics, exclude the last study
		dev1[i] <- (1 - equals(i, ns)) * (2 * (r1[i] * (log(r1[i]) - log(rhat1[i])) + (n1[i] - r1[i]) * (log(n1[i] - r1[i]) - log(n1[i] - rhat1[i]))))
		dev2[i] <- (1 - equals(i, ns)) * (2 * (r2[i] * (log(r2[i]) - log(rhat2[i])) + (n2[i] - r2[i]) * (log(n2[i] - r2[i]) - log(n2[i] - rhat2[i]))))
		resdev[i] <- dev1[i] + dev2[i]
		}

# Total residual deviance for model fit diagnostics
totresdev <- sum(resdev[])
# Prior distribution for the log risk ratio
d ~ dnorm(0, 0.0001)
# Calculate the risk ratio
RR <- exp(d)

# Prior distribution for the between-study variability (standard deviation)
tau ~ dunif(0, 1)
tau.sq <- tau * tau
# Convert standard deviation to precision
prec <- 1 / (tau.sq)
}

# Data for different transitions
# For 0 -> 1 transition (15 studies)
list(ns = 15)
r1[]	n1[]	r2[]	n2[]		# study id
41	1155	243	13613		# 1
47	420	116	478		# 2
17	158993	68	236693		# 3
297	79456	474	180790		# 4
3089	57604	4059	48706		# 5
6	466	22	1196		# 6
654	100751	644	83998		# 7
20	263	375	2949		# 8
20	215	327	2109		# 9
264	45285	415	45285		# 10
463	4105	1174	7119		# 11
5977	1614377	10827	1793698		# 12
95	6374	163	8500		# 13
10	273	243	2542		# 14
122	116247	1918	845906		# 15
END

# For 1 -> 2 transition (5 studies)
list(ns = 5)
r1[]	n1[]	r2[]	n2[]		# study id	
17	17	68	68		# 3
474	1831	719	2139		# 5
654	654	644	644		# 7
20	20	375	375		# 8
463	463	1174	1174		# 11
END

# For 2 -> 3 transition (3 studies)
list(ns = 3)
r1[]	n1[]	r2[]	n2[]		# study id					
2	17	21	68		# 3
161	1518	218	1638		# 5
14	654	22	644		# 7
END

# For 3 -> 4 transition (3 studies)
list(ns = 3)
r1[]	n1[]	r2[]	n2[]		# study id					
2	2	21	21		# 3
646	646	1195	1195		# 12
183	183	296	296		# 16
END

# For 4 -> 5 transition (2 studies)
list(ns = 2)
r1[]	n1[]	r2[]	n2[]		# study id				
0.5	2	0.5	21		# 3
57	183	72	296		# 16
END

# For 0 -> 3 transition (1 study)
list(ns = 1)
r1[]	n1[]	r2[]	n2[]		# study id			
183	402102	296	453015		# 16
END

# For 1 -> 3 transition (2 studies)
list(ns = 2)
r1[]	n1[]	r2[]	n2[]		# study id					
646	5977	1195	10827		# 12
24	122	273	1918		# 15
END

# For 1 -> 4 transition (1 study)
list(ns = 1)
r1[]	n1[]	r2[]	n2[]		# study id					
41	264	69	415		# 10
END

# For 3 -> 5 transition (1 study)
list(ns = 1)
r1[]	n1[]	r2[]	n2[]		# study id				
24	24	273	273		# 15
END

# Initial values
# Chain 1
list(d = 0)
# Chain 2
list(d = 1)
# Chain 3
list(d = -2)
# Chain 4
list(d = 6)
