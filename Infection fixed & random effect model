# Infection
# Pairwise meta-analysis
# Binomial likelihood, logit link
# Fixed effect model

# Model specification
model{
  	# Loop through each study
	for (i in 1:ns){
			 # Vague priors for the baseline infection rate of each study
			 mu[i] ~ dnorm(0, 0.0001)
			 # Loop through the two intervention groups
			 for (k in 1:2){
					# Binomial likelihood for the observed number of infections
					r[i, k] ~ dbin(p[i, k], n[i, k])
					# Linear predictor model for the log-odds of infection
					logit(p[i, k]) <- mu[i] + d[k]
					# Expected number of infections
					rhat[i, k] <- p[i, k] * n[i, k]
					# Residual deviance for each observation
					dev[i, k] <- 2 * (r[i, k] * (log(r[i, k]) - log(rhat[i, k])) 
							+ (n[i, k] - r[i, k]) * (log(n[i, k] - r[i, k]) - log(n[i, k] - rhat[i, k])))
					}
			# Sum of residual deviance for each study
			resdev[i] <- sum(dev[i, ])
			}
# Total residual deviance across all studies
totresdev <- sum(resdev[])
# Set treatment effect for the reference intervention (intervention 1) to zero
d[1] <- 0
# Vague prior for treatment effect of intervention 2
d[2] ~ dnorm(0, 0.0001)
# Take exponential to calculate OR for intervention 2 relative to intervention 1
OR <- exp(d[2])
# Probability that the LOR is positive
# Indicates if intervention 2 is less effective than intervention 1
prob.inferior <- step(d[2])					
}

# Infection data
list(ns = 15)
n[,1]	r[,1]	n[,2]	r[,2]
1155	41	13613	243
420	47	478	116
158993	17	236693	68
79456	297	180790	474
57604	3089	48706	4059
466	6	1196	22
100751	654	83998	644
263	20	2949	375
215	20	2109	327
45285	264	45285	415
4105	463	7119	1174
1614377	5977	1793698	10827
6374	95	8500	163
273	10	2542	243
116251	122	845906	1918
END

# Initial values
# Chain 1
list(d = c(NA, 0), mu = c(0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0))
# Chain 2
list(d = c(NA, -1), mu = c(-3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3))
# Chain 3
list(d = c(NA, -2), mu = c(1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1))

#################################################################################################################
#################################################################################################################

# Infection
# Pairwise meta-analysis
# Binomial likelihood, logit link
# Random effect model

# Model specification
model{
	# Loop through each study
	for (i in 1:ns){
			 # The treatment effect is set to zero for the intervention 1 (reference)
			 delta[i, 1] <- 0
			 # Vague priors for the baseline infection rate of each study
			 mu[i] ~ dnorm(0, 0.0001)
			 # Loop through the two intervention groups
			 for (k in 1:2){
					# Binomial likelihood for the observed number of infections
					r[i, k] ~ dbin(p[i, k], n[i, k])
					# Linear predictor model for the log-odds of infection
					logit(p[i, k]) <- mu[i] + delta[i, k]
					# Expected number of infections
					rhat[i, k] <- p[i, k] * n[i, k]
					# Residual deviance for each observation
					dev[i, k] <- 2 * (r[i, k] * (log(r[i, k]) - log(rhat[i, k])) 
							+ (n[i, k] - r[i, k]) * (log(n[i, k] - r[i, k]) - log(n[i, k] - rhat[i, k])))
					}
			# Sum of residual deviance for each study
			resdev[i] <- sum(dev[i, ])
			# Distribution of trial-specific LOR
			delta[i, 2] ~ dnorm(d[2], tau)
		}
# Total residual deviance across all studies
totresdev <- sum(resdev[])
# Set treatment effect for the reference intervention (intervention 1) to zero
d[1] <- 0
# Vague prior for treatment effect of intervention 2
d[2] ~ dnorm(0, 0.0001)
# Vague prior for the between-trial standard deviation
sd ~ dunif(0, 2)
# Between-trial precision = 1 / between-trial variance
tau <- pow(sd, -2)
# Take exponential to calculate OR for intervention 2 relative to intervention 1
OR <- exp(d[2])
# Probability that the LOR is positive
# Indicates if intervention 2 is less effective than intervention 1
prob.inferior <- step(d[2])					
}

# Infection data
list(ns = 15)
n[,1]	r[,1]	n[,2]	r[,2]
1155	41	13613	243
420	47	478	116
158993	17	236693	68
79456	297	180790	474
57604	3089	48706	4059
466	6	1196	22
100751	654	83998	644
263	20	2949	375
215	20	2109	327
45285	264	45285	415
4105	463	7119	1174
1614377	5977	1793698	10827
6374	95	8500	163
273	10	2542	243
116251	122	845906	1918
END

# Initial values
# Chain 1
list(d = c(NA, 0), sd = 1, mu = c(0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0, 0))
# Chain 2
list(d = c(NA, -1), sd = 0.1, mu = c(-3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3, -3))
# Chain 3
list(d = c(NA, -2), sd = 0.5, mu = c(1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1))
